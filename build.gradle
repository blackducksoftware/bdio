buildscript {
	repositories { jcenter() }
	dependencies { classpath 'com.netflix.nebula:nebula-dependency-recommender:3.3.0' }
	dependencies { classpath 'com.netflix.nebula:nebula-publishing-plugin:4.7.0' }
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'nebula.dependency-recommender'

	repositories {
		jcenter()
	}

	dependencyRecommendations {
		propertiesFile file: file("$rootDir/dependencies.properties")
	}

	eclipse {
		classpath {
			defaultOutputDir = file('build-eclipse')
		}
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'nebula.source-jar'
	apply plugin: 'nebula.maven-publish'

	sourceCompatibility = 1.7
	
	dependencies {
		testCompile 'junit:junit'
		testCompile 'com.google.truth:truth'
	}

	jar {
		manifest {
			attributes('Implementation-Title': project.description,
			           'Implementation-Version': project.version,
			           'Specification-Title': specTitle,
			           'Specification-Version': specVersion,
			           'Specification-Vendor': specVendor)
		}
	}
	
	publishing {
	    publications {
	        nebula(MavenPublication) {
	            pom.withXml {
	                configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
	                    asNode().dependencies[0].dependency.find {
	                        it.artifactId[0].text() == dep.moduleName && 
	                        it.groupId[0].text() == dep.moduleGroup
	                    }?.scope[0]?.value = 'compile'
	                }
	            }
	        }
	    }
	}
}

task wrapper(type: Wrapper) { gradleVersion = '3.0' }
