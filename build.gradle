buildscript {
	repositories { jcenter() }
	dependencies { classpath 'com.netflix.nebula:nebula-dependency-recommender:3.3.0' }
	dependencies { classpath 'com.netflix.nebula:nebula-publishing-plugin:4.6.0' }
}

allprojects {
	apply plugin: 'nebula.dependency-recommender'
	apply plugin: 'eclipse'

	repositories {
		jcenter()
	}

	dependencyRecommendations {
		propertiesFile file: file("$rootDir/dependencies.properties")
	}

	eclipse {
		classpath {
			defaultOutputDir = file('build-eclipse')
		}
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'nebula.maven-base-publish'
	apply plugin: 'nebula.maven-resolved-dependencies'
	apply plugin: 'nebula.source-jar'

	sourceCompatibility = 1.7
	
	dependencies {
		testCompile 'junit:junit'
		testCompile 'com.google.truth:truth'
	}

	jar {
		manifest {
			attributes('Implementation-Title': project.description,
			           'Implementation-Version': project.version,
			           'Specification-Title': specTitle,
			           'Specification-Version': specVersion,
			           'Specification-Vendor': specVendor)
		}
	}
}

task wrapper(type: Wrapper) { gradleVersion = '2.13' }
