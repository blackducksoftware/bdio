buildscript {
	repositories { maven { url 'https://plugins.gradle.org/m2/' } }
	dependencies { classpath 'com.netflix.nebula:nebula-dependency-recommender:3.6.3' }
	dependencies { classpath 'com.netflix.nebula:nebula-publishing-plugin:4.9.1' }
	dependencies { classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8' }
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'nebula.dependency-recommender'

	repositories {
		jcenter()
	}

	dependencyRecommendations {
		propertiesFile file: file("$rootDir/dependencies.properties")
	}

	eclipse {
		classpath {
			defaultOutputDir = file('build-eclipse')
		}
	}

	// There is a problem with the dependencies in Gremlin (Sqlg), this seems to solve the issue
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            // This 'jeremyh' fellow seems to have take the jBCrypt from mindrot.org and re-packaged it
            if (details.requested.group == 'com.github.jeremyh' && details.requested.name == 'jBCrypt' && details.requested.version == 'jbcrypt-0.4') {
                details.useTarget 'org.mindrot:jbcrypt:0.3m' // TODO Use the '0.4-atlassian-1' version from JCenter?
            }
        }
    }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'nebula.source-jar'
	apply plugin: 'nebula.maven-publish'
	apply plugin: 'net.ltgt.errorprone'

	sourceCompatibility = 1.8

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:all'
	}

	jar {
		manifest {
			attributes('Implementation-Title': project.description,
			           'Implementation-Version': project.version,
			           'Specification-Title': specTitle,
			           'Specification-Version': specVersion,
			           'Specification-Vendor': specVendor)
		}
	}
	
	publishing {
	    publications {
	        nebula(MavenPublication) {
	            pom.withXml {
	                configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
	                    asNode().dependencies[0].dependency.find {
	                        it.artifactId[0].text() == dep.moduleName && 
	                        it.groupId[0].text() == dep.moduleGroup
	                    }?.scope[0]?.value = 'compile'
	                }
	            }
	        }
	    }
	}
}
